# Creating our Default Response Lambda function
[ << Step 1](./1.md)

In the last step we deployed our API, but the response we got was `Not Found`.

Our API doesn't do anything on its own, we need to "wire" it to another service
to ingest the request, act upon it, and return a response.  In this case we're
creating a Lambda function that will say `Hello World` to us.

## About Lambda functions

A Lambda function is an isolated compute unit with a set of distinct properties:

- The OS is managed by AWS
- The Runtime is chosen by us, by managed by AWS
- Anything sent to StdOut or StdErr _can_ be delivered to CloudWatch Logs
- AWS libraries are present by default (most Lambda function _only_ interact
with AWS APIs directly, although that doesn't need to be the case)

The only thing we should be concerned with is:
- Our business logic
- Providing the required permissions for the code

The benefits are:

- A lot of the classic Ops work is managed by AWS (commonly
referred to as "undifferentiated heavy lifting")
- Code is restricted to do only what it is allowed to do, i.e. it is as secure
as we can make it

A Lambda function also has two phases of execution:
- Initialisation
- Execution

Initialisation only occurs when the Lambda function is first spawned.  This can
be used to set up global items

Execution happens for each execution request.  Lambda functions can only execute
for a maximum of 15 minutes, but may be re-used multiple times

By default, Lambda function do _not_ live in our VPC (`VPC` == `Virtual Private
Cloud`).  They live in a physically separate VPC owned and managed by AWS and as
such _cannot_ directly access any of our VPC-bound resources (i.e. databases etc
).  We _can_ ask AWS to launch our function inside of one of our VPCs however
this is slightly slower and should be avoided unless absolutely necessary.

Lambda functions also have internet accesss via a NAT gateway, again fully
managed by AWS.  This lets us avoid building complicated networking, and also
the costly effort of managing and debugging issues associated with networks.

Because of these two features, when developing code you should consider the
function to be running truly "In the Cloud", not in "Your Private Cloud".

## Creating the function code

Start by creating a new folder called `default_src` and inside the folder create a file
called `index.js`.  This is called the `handler` function.

The Lambda service passes an "event" object to the handler function which
provides the information the function may need in order to perform its logic.

In return we must tell the API how to respond, and at a minimum this includes:

- HTTP Status code
- Content-type header
- Body content

Add the following code the `index.js` file:

```
exports.handler = async (event) => {
  // Record the incomming event in the CloudWatch Log
  console.log(event)

  // Return our response to API Gateway
  return {
    statusCode: 200,
    headers: {
      'Content-Type': 'text/html'
    },
    body: '<html><body><h1>Hello World</h1></body></html>'
  }
}
```

This function is extremely simple, but acts as a great starting point for an API
handler function.  It will return a 200 (OK) response, a `content-type` of
`text/html`, and a small HTML body for the browser to display.

## Creating the AWS Resources

To upload a function to a Lambda function we need to create a zip file.

Create a new fils called `default_lambda.tf` and add the following resource:

```
data "archive_file" "default_lambda" {
  type        = "zip"
  output_path = "${path.module}/.terraform/default_lambda.zip"
  source_dir  = "./default_src"
}
``` 

This tells Terraform to create a zip file of the `./src` directory, and store it
in the `.terraform` directory with the name `default_lambda.zip`

Next we need to create a Role which allows the Lambda service to access our
service:

```
resource "aws_iam_role" "default_lambda" {
  name               = "default_lambda_role"
  assume_role_policy = <<-EOF
    {
      "Version": "2012-10-17",
      "Statement": [
        {
          "Effect": "Allow",
          "Action": "sts:AssumeRole",
          "Principal": {
            "Service": "lambda.amazonaws.com"
          }
        }
      ]
    }
  EOF
}
```

Finally we can create our actual function!

```
resource "aws_lambda_function" "default_lambda" {
  function_name    = "${var.api_name}-default-lambda"
  filename         = data.archive_file.default_lambda.output_path
  source_code_hash = data.archive_file.default_lambda.output_base64sha256

  runtime = "nodejs12.x"
  handler = "index.handler"

  role = aws_iam_role.default_lambda.arn
}
```

Notice how in the function resource itself we do not specifically name the zip
file, it's hash, or the name of our Role.  Instead we reference the output from 
the resources that create our dependencies.  Terraform uses these references to
create its dependency graph, which ensures resources are created (or destroyed)
in the right order.  This is an important part of how Terraform operates. 

We also add the `api_name` variable value in to our function name.  In this case
it ensure we don't clash with each other, but it also serves to help us manually
track what service resources belong to.

Finally the `handler` parameter `index.handler` tells Lambda to import our code,
and to execute a function called `handler` in the file `index.js`

## Deploy your function

You can now use `make` and `make apply` to deploy your code.  Remember to check
what Terraform is telling you to make sure it is what you expect.

## Testing our function

Terraform will upload whatever code you supply & AWS will let you upload
whatever you want, but that doesn't mean the code will work!

In the AWS console, navigate to the Lambda console and click your function.  It
should be called `<your_name>-default-lambda`

In the top-right there is a `Test` button, and next to it is a drop-down to
select the test you want to run.  Since you dont have any defined, click the
drop-down and click `Configure test events`.

Here you can add an event structure which will be passed to your Lambda function
so you can examine its output.  You can use the default `hello-world`, so give
it a name, and click `Create`

Back in the Lambda function console, click the `Test` button.  If execution was
successful you should see a green `Execution result: succeeded` section.  If
there were errors it will be red and show you the output.

At this point, if you have any issues, resolve them in the function code, and
re-deploy to AWS.

Once you have a successful response, move on to the next section

[Step 3 - Attach the Lambda function to the API Route](./3.md)